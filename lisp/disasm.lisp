;;; disasm.lisp
;;;
;;; Copyright (C) 2006-2011 Peter Graves <gnooth@gmail.com>
;;;
;;; This program is free software; you can redistribute it and/or
;;; modify it under the terms of the GNU General Public License
;;; as published by the Free Software Foundation; either version 2
;;; of the License, or (at your option) any later version.
;;;
;;; This program is distributed in the hope that it will be useful,
;;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;;; GNU General Public License for more details.
;;;
;;; You should have received a copy of the GNU General Public License
;;; along with this program; if not, write to the Free Software
;;; Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.

(in-package "DISASSEMBLER")

(defstruct (disassembly-block (:conc-name "BLOCK-"))
  start-address
  end-address
  instructions)

(defstruct operand
  kind ; :register, :indirect, :immediate, :relative, :absolute
  register ; base register
  index
  scale
  data)

(defknown make-register-operand (t) operand)
(defun make-register-operand (reg)
  (make-operand :kind :register
                :register reg))

(defknown make-indirect-operand (t) operand)
(defun make-indirect-operand (reg)
  (make-operand :kind :indirect
                :register reg))

(defknown make-immediate-operand (t) operand)
(defun make-immediate-operand (data)
  (make-operand :kind :immediate
                :data data))

(defknown make-absolute-operand (t) operand)
(defun make-absolute-operand (data)
  (make-operand :kind :absolute
                :data data))

(defstruct instruction
  start
  length
  mnemonic
  operand1
  operand2
  annotation)

(defparameter *disassemblers* (make-array 256 :initial-element nil))

(declaim (type (simple-array t (256)) *disassemblers*))

(defun install-disassembler (byte disassembler)
  (declare (type (integer 0 255) byte))
  (declare (type symbol disassembler))
  (setf (svref *disassemblers* byte) disassembler))

(defun find-disassembler (byte)
  (declare (type (integer 0 255) byte))
  (svref *disassemblers* byte))

(defmacro define-disassembler (byte-or-bytes &body body)
  (let* ((bytes (designator-list byte-or-bytes))
         (name (intern (format nil "DIS铂О佝怡翦螬┅ㄡ蜱Ж怡翦篝狎痱彐轼怡翦┅啜痱镧ㄤ彐躅钺礤狎珞ㄤ邈灬蝈ㄩ珙矧徕戾泪蜱螬戾眍屙镱殂戾铉翳镳弪犷浔镳弪犷洳犷铒翎糸镱棱镤麒孱痱彐轼怡翦ㄤ邈篝狎舂ㄩ钽戾铉翳┅磲脲轭篝蝓泗轱后翎螋篝狎红孱玺戾铉翳喉铄盹铋眍屙镱殂猴疱蜥钿镳弪犷浔猴疱蜥钿镳弪犷洳横铑雉狒轱犷铒翎糸镱┅ㄤ镬轶ㄢ翦К怡翦螬ㄩ铙翎祆溟筢篌屙忪弪怡翦К钺礤┅┅ㄤ彐疳蜥礤翦赭锃怡翦溟筢篌屙忪弪螵磲脲栳箬翎忪呼弩у聃犰┅ㄤ邈灬轫豉疱栳箬翎忪赭锃怡翦溟筢篌屙忪弪螵┅ㄤ彐躅轭篝犰飙赭锃怡翦溟筢篌屙忪弪ㄢ翦溟筢篌屙忪弪箦翩ㄧ弭栳箬怡翦赭锃怡翦溟筢篌屙忪弪螵溟筢篌屙忪弪┅ㄤ彐躅骈钿赭锃怡翦溟筢篌屙忪弪ㄢ翦怡翦博ㄧ弭栳箬扉篝怡翦怡翦博赭锃怡翦溟筢篌屙忪弪螵┅ㄤ彐磲泸溴骈铄赭锃怡翦溟筢篌屙忪弪ㄦ轵篝怡翦箦泔钿怡翦矧怡翦怙澌怙澌戾舄è箦泔钿怡翦ㄤ弩殓钺麸颦扉篝箦泔钿怡翦矧怡翦螬钺礤ㄩ铘弪ㄦ矧磲铋⒛捎铂О佝铂О佝骈蝮舡怡翦箦泔钿怡翦螬┅ㄡ蜱Ж怡翦怡翦篝狎痱彐轼怡翦┅啜痱镧ㄤ彐躅钺礤狎珞ㄤ邈灬蝈ㄩ珙矧徕戾泪蜱螬戾眍屙镱殂戾铉翳镳弪犷浔镳弪犷洳犷铒翎糸镱棱镤麒孱痱彐轼怡翦ㄤ邈篝狎舂ㄩ钽戾铉翳┅磲脲轭篝蝓泗轱后翎螋篝狎红孱玺戾铉翳喉铄盹铋眍屙镱殂猴疱蜥钿镳弪犷浔猴疱蜥钿镳弪犷洳横铑雉狒轱犷铒翎糸镱┅ㄤ镬轶箦泔钿怡翦К箦泔钿怡翦螬ㄩ铙翎祆赭锃怡翦溟筢篌屙忪弪扉篝骈蝮舡怡翦箦泔钿怡翦К钺礤┅┅ㄤ彐躅躅篚痧矧翦īㄥ蝌矧Ⅴ铙躔痫螋邃┅ㄤ彐躅躅篚痧矧翦洵怡翦箦聃孱沐é蝈篝怡翦螬ㄩ戾铉翳羼怡翦暴ㄥ蝌矧Ⅴ铙躔痫螋邃镳泔溴ｘ铂Оē汜怡翦螬ㄥ蝌矧Ⅴ铙躔痫螋邃怡翦箦聃孱沐ｘ铂О" bytes)))

(defmacro with-modrm-byte (byte &body body)
  `(let ((modrm-byte ,byte))
     (declare (type (unsigned-byte 8) modrm-byte))
     (let ((mod (ldb (byte 2 6) modrm-byte))
           (reg (ldb (byte 3 3) modrm-byte))
           (rm  (ldb (byte 3 0) modrm-byte)))
       (declare (ignorable mod reg rm))
       ,@body)))

(defmacro with-sib-byte (byte &body body)
  `(let ((sib-byte ,byte))
     (declare (type (unsigned-byte 8) modrm-byte))
     (let ((scale (ldb (byte 2 6) sib-byte))
           (index (ldb (byte 3 3) sib-byte))
           (base  (ldb (byte 3 0) sib-byte)))
       (declare (ignorable scale index base))
       ,@body)))
